;;;
;;; MOST (not all) of the parameters available to the 'analogue' synth
;;;

;; set everything to defaults!

;; MAIN:
;; portamento only in overlapping monophonic playback
;; inst, gain(0.0-3.0), pan(0.0-1.0), portamento(ms), legato(LEGATO_ON/LEGATO_OFF)
(set_main syn1 2.0 0.5 100.0 LEGATO_ON)
(set_polyphony syn1 16) ;; (1-32), 1 for mono synth

;; OSCILLATORS: * 4
;; where tuning is a frequency(hz) relation to incoming pitch
;; i.e. 1.0 is no change, 0.5 is octave below, 2.0 is octave above, 1.0029237 is a slight detune etc.
;; inst, osc#(1-4), gain(0.0-2.0), wave(PARAM_(SAW|TRI|PULSE|TRI)), tuning(0.0-16.0), pulse width(0.0-1.0), phase(0.0-1.0)
(set_osc syn1 1 0.5 PARAM_SAW 1.0 0.0 0.0)

;; ENVELOPES: 
;; inst, attack(ms), decay(ms), sustain(0.0-1.0), release(ms)
(set_amp_env syn1 0.0 0.0 1.0 0.0)
(set_filter_env syn1 0.0 0.0 1.0 0.0)

;; LOW PASS FILTER: 
;; inst, frq(hz), resonance(0.0-1.0), drive(0.0-3.0), db slope(12|24), amount of envelope(hz), track frq(0.0-1.0), track env(0.0-1.0)
(set_filter syn1 18000.0 0.0 0.0 24 0.0 0.0 0.0)

;; DELAY: 
;; inst, mix(0.0-1.0), left-delay(ms), right-delay(ms), feedback(0.0-1.0), lpf cutoff(hz), lpf Q(0.0-1.0)
(set_delay syn1 0.0 (/ 1000.0 5) (/ 1000.0 7) 0.5 18000.0 0.0)

;; REVERB: 
;; inst, mix(0.0-1.0), size(0.0-1.0), pre delay(ms), absorb(0.0-1.0)
(set_reverb syn1 0.0 0.2 75.0 0.1)

;; FLANGER:
;; inst, mix(0.0-1.0), low delay(ms), high delay(ms), rate(hz), feedback(0.0-1.0)
(set_flanger syn1 0.0 5.0 8.0 0.123 0.0)

;; NOISE:
;; inst, gain(0.0-1.0+)
(set_noise_amp syn1 0.0)
;; inst PARAM_PINK | PARAM_WHITE
(set_noise_type syn1 PARAM_PINK)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; OK let's start a basic pattern to get
;; some noise happening.
(:> A 4 0 (play syn1 @1 80 dur) (zip (scale 3 8) (rot -2 (rev (scale 4 8)))))

;; first classic step is to introduce
;; second detuned oscillator
;; see above for params of set_osc 
(set_osc syn1 2 0.4 PARAM_SAW 1.004928 0.0 0.0)

;; next let's play with the amp envelope
;; let's add some wah
(set_amp_env syn1 60.0 100.0 0.8 100.0)

;; let's bring in the filter
(set_filter syn1 5000.0 0.2 1.0 12 0.0 0.0 0.0)
;; add some filter envelope
;; you will not hear this until we add below
(set_filter_env syn1 40.0 100.0 0.5 100.0)
;; now 'add' the envelope to the filter cutoff
(set_filter syn1 0.0 0.3 0.5 12 3500.0 0.1 0.0)

;; add a sub octave 
(set_osc syn1 3 0.4 PARAM_SINE 0.499928 0.0 0.0)

;; add some 'chorus'
;; fatten up and add some breadth
(set_flanger syn1 0.4 5.0 8.0 0.129323 0.0)
;; and a little stereo delay
(set_delay syn1 0.2 (/ 500.0 6) (/ 500.0 4) 0.6 5000.0 0.0)

;; and bring up the overall gain
(set_main syn1 3.0 0.5 100.0 LEGATO_ON)

;; and now add a second pattern to 'sweep' the filter
(:> B 4 0 (set_filter_env syn1 40.0 100.0 (trir 0.0 1.0 1/64) 100.0) (nof 16 0))

;; finally get a little dirty?
(set_filter syn1 0.0 0.45 3.0 12 3500.0 0.15 0.0)

;; four on the floor!
(:> C 4 -1 (play samp1 @1 110 dur) `(bd4 #(bd4 sn) bd4 #(bd4 sn)))
(:> D 4 0 (play samp1 @1 (cosr 110 20 (random '(7/5 2))) dur) (jumble `(,@(nof 8 'ch) ,@(nof 4 'ch3) ,@(nof 4 'ch2))))

;; a little reverb to finish!
(main_reverb 1 0.1) ;; syn1
(main_reverb 4 0.1) ;; samp1
